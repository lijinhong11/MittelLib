plugins {
    id "java"
    id "maven-publish"
    id "org.jreleaser" version "1.22.0"
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = "io.github.lijinhong11"
version = properties["version"]

repositories {
    mavenCentral()
    maven {
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = "https://maven.devs.beer/"
    }
    maven {
        url = "https://repo.nexomc.com/releases"
    }
    maven {
        url = "https://repo.momirealms.net/releases/"
    }
    maven {
        url = "https://repo.oraxen.com/releases"
    }
    maven {
        url = "https://mvn.lumine.io/repository/maven-public/"
    }
    maven {
        url = "https://nexus.phoenixdevt.fr/repository/maven-public/"
    }
    maven {
        url = "https://api.modrinth.com/maven/"
    }
    maven {
        url = "https://repo.auxilor.io/repository/maven-public/"
    }
    maven {
        url = "https://repo.extendedclip.com/releases/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.11-R0.1-SNAPSHOT")

    //libraries
    implementation("org.reflections:reflections:0.10.2")

    //placeholders
    compileOnly("me.clip:placeholderapi:2.12.2")
    compileOnly("io.github.miniplaceholders:miniplaceholders-api:3.1.0")

    //content provider
    compileOnly("dev.lone:api-itemsadder:4.0.10")

    compileOnly("com.nexomc:nexo:1.16.1")

    compileOnly("net.momirealms:craft-engine-core:0.0.67")
    compileOnly("net.momirealms:craft-engine-bukkit:0.0.67")

    compileOnly("io.th0rgal:oraxen:1.207.0")

    compileOnly("net.Indyuce:MMOItems-API:6.10.1-SNAPSHOT")
    compileOnly("io.lumine:MythicLib-dist:1.7.1-SNAPSHOT")

    compileOnly("maven.modrinth:SCore:5.25.7.19")

    compileOnly("com.willfp:eco:6.77.3")
    compileOnly("com.willfp:EcoItems:5.66.0")
    compileOnly("com.willfp:libreforge:4.79.0:all")

    //lombok
    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.20.1")
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    filteringCharset "UTF-8"
    filesMatching("plugin.yml") {
        expand project.properties
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                groupId = "io.github.lijinhong11"
                artifactId = "MittelLib"
                version = this.version
                description = "A library for Mittel series"
                name = "MittelLib"
                url = "https://github.com/lijinhong11/MittelLib"

                licenses {
                    license {
                        name = "The GNU General Public License, Version 3.0"
                        url = "https://www.gnu.org/licenses/gpl-3.0.md"
                    }
                }
                developers {
                    developer {
                        id = "lijinhong11"
                        name = "Jinhong Li"
                        email = "tygfhk@outlook.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/lijinhong11/MittelLib.git"
                    developerConnection = "scm:git:ssh://github.com/lijinhong11/MittelLib.git"
                    url = "https://github.com/lijinhong11/MittelLib"
                }
            }

            from(components.java)
        }
    }

    repositories {
        maven {
            url = uri(layout.projectDirectory.dir("repo"))
        }
    }
}


jreleaser {
    signing {
        pgp {
            active = 'ALWAYS'
            armored = true

            mode = "COMMAND"

            command {
                executable = "gpg"
            }
        }
    }

    deploy {
        maven {
            mavenCentral {
                sonatype {
                    applyMavenCentralRules = true

                    username = System.getenv("SONATYPE_USERNAME")
                    password = System.getenv("SONATYPE_PASSWORD")

                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('repo')
                }
            }
        }
    }
}